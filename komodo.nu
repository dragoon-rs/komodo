const KOMODO_BINARY = "./target/release/komodo"
const DEFAULT_LOG_LEVEL = "INFO"

def home-dir []: nothing -> path {
    $env.KOMODO_HOME? | default (
        $env.XDG_DATA_HOME? | default "~/.local/share" | path join "komodo"
    ) | path expand
}

def block-dir []: nothing -> path {
    home-dir | path join "blocks"
}

def "nu-complete log-levels" []: nothing -> list<string> {
    [
        "TRACE"
        "DEBUG",
        "INFO",
        "WARN",
        "ERROR",
    ]
}

def run-komodo [
    --input: path = "",
    --nb-bytes: int = 0,
    -k: int = 0,
    -n: int = 0,
    --generate-powers,
    --reconstruct,
    --verify,
    --combine,
    --inspect,
    --log-level: string,
    ...block_hashes: string,
]: nothing -> any {
    let home_dir = home-dir
    if not ($home_dir | is-empty) {
        mkdir $home_dir
    }
    let block_dir = block-dir
    if not ($block_dir | is-empty) {
        mkdir $block_dir
    }

    with-env {RUST_LOG: $log_level} {
        let res = do {
            ^$KOMODO_BINARY ...([
                $input
                $k
                $n
                ($generate_powers | into string)
                $home_dir
                ($reconstruct | into string)
                ($verify | into string)
                ($combine | into string)
                ($inspect | into string)
                $nb_bytes
            ] | append $block_hashes)
        } | complete

        print --no-newline $res.stdout
        if $res.exit_code != 0 {
            error make --unspanned { msg: $"($res.stderr) \(($res.exit_code)\)" }
        }
        $res.stderr | from json
    }
}

def list-blocks []: nothing -> list<string> {
    try {
        ls (block-dir) | get name | path parse | get stem
    } catch {
        []
    }
}

export def "komodo build" []: nothing -> nothing {
    ^cargo build --package komodo --release
}

export def "komodo setup" [
    nb_bytes: int,
    --log-level: string@"nu-complete log-levels" = $DEFAULT_LOG_LEVEL
]: nothing -> nothing {
    (
        run-komodo
            --log-level $log_level
            --nb-bytes $nb_bytes
            --generate-powers
    )
}

export def "komodo prove" [
    input: path,
    --fec-params: record<k: int, n: int>,
    --log-level: string@"nu-complete log-levels" = $DEFAULT_LOG_LEVEL
]: nothing -> list<string> {
    (
        run-komodo
            --log-level $log_level
            --input $input
            -k $fec_params.k
            -n $fec_params.n
    )
}

export def "komodo verify" [
    ...blocks: string@"list-blocks",
    --log-level: string@"nu-complete log-levels" = $DEFAULT_LOG_LEVEL
]: nothing -> table<block: string, status: int> {
    run-komodo --log-level $log_level --verify ...$blocks
}

export def "komodo reconstruct" [
    ...blocks: string@"list-blocks",
    --log-level: string@"nu-complete log-levels" = $DEFAULT_LOG_LEVEL
]: nothing -> list<int> {
    run-komodo --log-level $log_level --reconstruct ...$blocks
}

export def "komodo combine" [
    ...blocks: string@"list-blocks",
    --log-level: string@"nu-complete log-levels" = $DEFAULT_LOG_LEVEL
]: nothing -> string {
    run-komodo --log-level $log_level --combine ...$blocks | get 0
}

export def "komodo inspect" [
    ...blocks: string@"list-blocks",
    --log-level: string@"nu-complete log-levels" = $DEFAULT_LOG_LEVEL
]: nothing -> table<shard: record<k: int, comb: list<any>, bytes: list<string>, hash: string, size: int>, commits: list<string>, m: int> {
    run-komodo --log-level $log_level --inspect ...$blocks
}

export def "komodo ls" []: nothing -> list<string> {
    list-blocks
}

export def "komodo clean" []: nothing -> nothing {
    rm --force --recursive (home-dir)
}

def pretty-code []: string -> string {
    $"`(ansi default_dimmed)($in)(ansi reset)`"
}

export def main []: nothing -> nothing {
    let help = [
        $"the location of the files generated by Komodo can be configured via ("$env.KOMODO_HOME" | pretty-code) which will default to",
        $"- ("$env.XDG_DATA_HOME/komodo" | pretty-code) if ("$env.XDG_DATA_HOME" | pretty-code) is set",
        $"- ("~/.local/share/komodo/" | pretty-code) otherwise"
    ]

    print ($help | str join "\n")
}
